Assignment 5 SPARQL queries

This document was created by Alejandro García García and contains the final answers for the assignment 5 of the subject “Bioinformatic Programming Challenges”.
-----------------------------------------

UNIPROT SPARQL PART:

UniProt SPARQL Endpoint:  http://sparql.uniprot.org/sparql/
-----

1) How many protein records are in UniProt? 

PREFIX core:<http://purl.uniprot.org/core/>

SELECT (COUNT(DISTINCT ?prot) AS ?uniprot_prot_records) #IMPORTANT "DISTINCT" IF YOU WANT TO COUNT PROPERLY
WHERE
{
	?prot a core:Protein #core:Protein is the core class in Uniprot
}

This one takes me a lot of time because the solution appeared after 20 minutes and I thought I was wrong. 


2) How many Arabidopsis thaliana protein records are in UniProt? 

PREFIX core:<http://purl.uniprot.org/core/> 
PREFIX taxonomy:<http://purl.uniprot.org/taxonomy/>

SELECT (COUNT(DISTINCT ?prot) AS ?uniprot_prot_records) #We need to add DISTINCT as well
WHERE
{
	?prot a core:Protein .
	?prot core:organism taxonomy:3702 
}

http://purl.uniprot.org/taxonomy/ is used to find the organisms. Then we have to specify the ID of Arabidopsis. This ID is 3702. 


3) What is the description of the enzyme activity of UniProt Protein Q9SZZ8 

PREFIX core:<http://purl.uniprot.org/core/>
PREFIX uniprotkb:<http://purl.uniprot.org/uniprot/>  
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#> 

SELECT DISTINCT ?enzyme_name ?activity_description 
WHERE
{
  uniprotkb:Q9SZZ8 a core:Protein .
  uniprotkb:Q9SZZ8 core:enzyme ?enzyme . 
  uniprotkb:Q9SZZ8 core:annotation ?function .
  
  ?function a core:Function_Annotation .
  ?function rdfs:comment ?activity_description .
  
  ?enzyme skos:prefLabel ?enzyme_name 
}


It first tell us that is an enzyme, and after that we want to know its name and its activity.


4) Retrieve the proteins ids, and date of submission, for proteins that have been added to UniProt this year   (HINT Google for “SPARQL FILTER by date”)

PREFIX core:<http://purl.uniprot.org/core/>
PREFIX xsd:<http://www.w3.org/2001/XMLSchema#>

SELECT ?prot_id ?date  #We have to SELECT both at the same time

WHERE 
{ 
	?prot_id a core:Protein .
	?prot_id core:created ?date .
	
	FILTER (?date >= xsd:date("2018-01-01")) 
}


5) How  many species are in the UniProt taxonomy?

PREFIX core:<http://purl.uniprot.org/core/>

SELECT (COUNT(DISTINCT ?taxon) AS ?species_number)
FROM <http://sparql.uniprot.org/taxonomy> 
WHERE
{
	?taxon a core:Taxon .
  	?taxon core:rank core:Species
}

#We specified from where we want to extract the Species. FROM line is necessary.


6)  How many species have at least one protein record?

#It is quite similar to the previous one

PREFIX core:<http://purl.uniprot.org/core/>

SELECT (COUNT(DISTINCT ?taxon) AS ?species_number)

WHERE
{
	?prot a core:Protein .
  	?prot core:organism ?taxon .
	
  	?taxon a core:Taxon .
	?taxon core:rank core:Species 
}

#We do not use a filter in this case, because if they have proteins it means that they have at least one protein. 


----------------------

ATLAS PART

Atlas gene expression database SPARQL Endpoint: http://www.ebi.ac.uk/rdf/services/atlas/sparql
-----

7) What is the Affymetrix probe ID for the Arabidopsis Apetala3 gene?  (HINT - you cannot answer this directly from Atlas - you will first have to look at what kinds of database cross-references are in Atlas, and then construct the appropriate URI for the Apetala3 gene based on its ID number in *that* database)

I tried to do this query as you can see below

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX core:<http://purl.uniprot.org/core/> 
PREFIX taxonomy:<http://purl.uniprot.org/taxonomy/>
PREFIX atlasterms: <http://rdf.ebi.ac.uk/terms/atlas/>

SELECT ?id
WHERE
{
	?prot a core:Protein .
	?prot core:organism taxonomy:3702 .
    ?prot core:recommendedName ?name .
  	?name core:fullName ?full .
  	FILTER CONTAINS( ?full, 'APETALA 3') .
	SERVICE<http://www.ebi.ac.uk/rdf/services/atlas/sparql> 
    {
      ?probe atlasterms:dbXref ?protein .
      ?probe dcterms:identifier ?id
    }
}

#However, I did not have an answer. So I tried to do it in another way (my partners helped me a bit)
#The answer is the next

PREFIX atlasterms:<http://rdf.ebi.ac.uk/terms/expressionatlas/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>

SELECT  DISTINCT ?probe_ID
WHERE {

  ?gene rdfs:label 'AP3' . #We first define that the genes we want to take into account is AP3 - APETALA 3 
  ?gene dcterms:identifier ?gene_ID . #Here we want to see the geneID to filter by AT - Arabidopsis thaliana
  
  ?expression atlasterms:refersTo ?gene . #we see the expression of the genes of AT 
  ?expression atlasterms:refersTo ?probe . #We just have to filter by Afflymetrix - AFFY

  ?probe rdfs:label ?probe_ID . 
  
  FILTER CONTAINS(str(?gene_ID), "AT") .
  FILTER CONTAINS(str(?probe), "AFFY") .
  
}


8) Get the experimental description for all experiments where the Arabidopsis Apetala3 gene is DOWN regulated 

PREFIX atlasterms:<http://rdf.ebi.ac.uk/terms/expressionatlas/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?exp_descr ?tstat
WHERE {
   ?gene rdfs:label 'AP3' . #This is the part where I have problems to start --> think properly Alejandro

   ?expression  atlasterms:refersTo   ?gene .
   ?expression  atlasterms:isOutputOf ?analysis .
   ?expression  atlasterms:tStatistic ?tstat .
    
   ?analysis  rdfs:label    ?exp_descr .
	
   FILTER(?tstat < 0) . #This indicates that it is down regulated
}

#In this case I search first for the gene AP3 and after I use these genes to look for its experiments.
#I do not know why the program does not let me to change ?tstat --> ?regulation. Every time I change it I had no answer





----------------------------------

REACTOME PART

REACTOME database SPARQL endpoint: http://www.ebi.ac.uk/rdf/services/reactome/sparql

------

9) How many REACTOME pathways are assigned to Arabidopsis (taxon 3702)? (note that REACTOME uses different URLs to define their taxonomy compared to UniProt, so you will first have to learn how to structure those URLs….)

PREFIX biopax3: <http://www.biopax.org/release/biopax-level3.owl#>
PREFIX taxonomy:<http://identifiers.org/taxonomy/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT (COUNT (DISTINCT ?path) AS ?reactome_number)
WHERE 
{
	?path a biopax3:Pathway . 
	?path biopax3:organism taxonomy:3702
}

I made it in a easy way, just counting the number of pathways having define a COUNT DISTINT ?path


10) Get all PubMed references for the pathway with the name “Degradation of the extracellular matrix”

PREFIX biopax3: <http://www.biopax.org/release/biopax-level3.owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT ?pubmed_references

WHERE
{
	?pathway a biopax3:Pathway . 
	?pathway biopax3:displayName ?name . 
	?pathway biopax3:xref ?pubmed_references .
	
	?pubmed_references biopax3:db ?db . 
	?pubmed_references biopax3:id ?id.
	
	FILTER(str(?name) = 'Degradation of the extracellular matrix') .
	FILTER(str(?db) ='Pubmed') 
}

We have to filter by Pubmed databse and by the pathway “Degradation of the extracellular matrix”, of course. 

---------------------------------------

BONUS QUERIES

------

UniProt BONUS) Find the AGI codes and gene names for all Arabidopsis thaliana  proteins that have a protein function annotation description that mentions “pattern formation”

PREFIX core:<http://purl.uniprot.org/core/>
PREFIX taxonomy:<http://purl.uniprot.org/taxonomy/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>

SELECT ?agi ?gene_name
WHERE
{
    ?prot a core:Protein .
    ?prot core:organism taxonomy:3702 .
	?prot core:annotation  ?function. 
    ?prot core:encodedBy ?gene .
	
    ?gene core:locusName ?agi . 
    ?gene skos:prefLabel ?gene_name . 

    
    ?function a core:Function_Annotation ; 
        rdfs:comment ?annotation . 

    FILTER CONTAINS(str(?annotation), 'pattern formation')      
}



REACTOME BONUS) Write a query that proves that all Arabidopsis pathway annotations in Reactome are “inferred from electronic annotation” (evidence code)  (...and therefore are probably garbage!!!)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX biopax3: <http://www.biopax.org/release/biopax-level3.owl#>
PREFIX taxonomy: <http://identifiers.org/taxonomy/>

SELECT(COUNT (DISTINCT ?total) AS ?total_proves) (COUNT (DISTINCT ?electronic) AS ?electronic_proves) 
WHERE
{
    ?path a biopax3:Pathway .
    ?path biopax3:organism taxonomy:3702 . 
    ?path biopax3:evidence ?total .  
    ?path biopax3:evidence ?electronic .
  
    ?total biopax3:evidenceCode ?ev_total .
    ?electronic biopax3:evidenceCode ?ev_electronic .

    ?ev_total biopax3:term ?term_total .
    ?ev_electronic biopax3:term ?term_electronic .

    FILTER (str(?term_electronic) = 'inferred from electronic annotation') 
}
